varnishtest "This is my first test"

server s1 {
  rxreq
  txresp

  accept

  rxreq
  txresp

  accept

  rxreq
  txresp

  accept

} -start

varnish v1 -vcl+backend {
  include "${pwd}/devicedetect.vcl";

  sub vcl_recv {
      call devicedetect;
  }

  sub vcl_fetch {
    # Add or extend the current Vary header with information of
    # the device that is used.
    if (req.http.X-UA-Device) {
      if (!beresp.http.Vary) { # no Vary at all
        set beresp.http.Vary = "X-UA-Device";
      } elseif (beresp.http.Vary !~ "X-UA-Device") { # add to existing Vary
        set beresp.http.Vary = beresp.http.Vary + ", X-UA-Device";
      }
    }

    # simplify response for testing to what we actually use
    if (req.http.X-UA-Device ~ "mobile") {
      set req.http.X-UA-Device = "mobile";
    }

    if (req.http.X-UA-Device ~ "tablet") {
      set req.http.X-UA-Device = "tablet";
    }

    # Add device information to the response.
    set beresp.http.X-UA-Device = req.http.X-UA-Device;
  }

  sub vcl_deliver {
    # To keep any caches in the wild from serving wrong content to client #2 behind them, we need to
    # transform the Vary on the way out.
    if (req.http.X-UA-Device && resp.http.Vary) {
      set resp.http.Vary = regsub(resp.http.Vary, "X-UA-Device", "User-Agent");
    }
  }

} -start

# Testing Google User-Agent-Strings
client c1 {
  txreq -hdr "User-Agent: SAMSUNG-SGH-E250/1.0 Profile/MIDP-2.0 Configuration/CLDC-1.1 UP.Browser/6.2.3.3.c.1.101 (GUI) MMP/2.0 (compatible; Googlebot-Mobile/2.1; +http://www.google.com/bot.html)"
  rxresp
  expect resp.status == 200
  expect resp.http.X-UA-Device == "mobile"

  txreq -hdr "User-Agent: DoCoMo/2.0 N905i(c100;TB;W24H16) (compatible; Googlebot-Mobile/2.1; +http://www.google.com/bot.html)"
  rxresp

  expect resp.status == 200
  expect resp.http.X-UA-Device == "mobile"

  txreq -hdr "User-Agent: Mozilla/5.0 (iPhone; CPU iPhone OS 6_0 like Mac OS X) AppleWebKit/536.26 (KHTML, like Gecko) Version/6.0 Mobile/10A5376e Safari/8536.25 (compatible; Googlebot/2.1; +http://www.google.com/bot.html)"
  rxresp
  expect resp.status == 200
  expect resp.http.X-UA-Device == "mobile"

} -run

varnish v1 -expect client_req == 3
